#!/usr/bin/env perl

use strict;
use warnings;
use Paws;

my $cli_arg = shift @ARGV or die "Usage: $0 PREFIX:region command";
my @to_exec = @ARGV or die "Usage: $0 PREFIX:region command";

my ($prefix, $region) = split /\:/, $cli_arg, 2;
die "first argument to $0 has to be formatted as PREFIX:region" if (not defined $region);

my $ssm = Paws->service('SSM', region => $region);

my @parameters;
my $result = $ssm->GetParametersByPath(
  MaxResults => 10,
  Path => "/$prefix/",
  WithDecryption => 1,
);
push @parameters, @{ $result->Parameters };

while (my $nt = $result->NextToken) {
  $result = $ssm->GetParametersByPath(
    MaxResults => 10,
    Path => "/$prefix/",
    WithDecryption => 1,
    NextToken => $nt,
  );
  push @parameters, @{ $result->Parameters };
}

die "Didn't find any parameters with prefix /$prefix/" if (not @parameters);

foreach my $param (@parameters) {
  my ($env_var) = ($param->Name =~ m/^\/$prefix\/(.*)$/);

  $ENV{ $env_var } = $param->Value;
}

exec @to_exec;
